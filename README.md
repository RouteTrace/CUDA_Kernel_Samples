# CUDA_Kernel_Samples
## å¼•è¨€
æœ¬é¡¹ç›®æ˜¯ CUDA **ç®—å­æ‰‹æ’•ä¸é¢è¯•æŒ‡å—**ï¼š
1. æ±‡æ€»äº†é¢è¯•é«˜é¢‘çš„ CUDA ç®—å­é¢˜ç›®å’Œä¼˜åŒ–ç­–ç•¥ï¼ŒåŒ…å«é¢è¯•é«˜é¢‘ç®—å­çš„ç¼–å†™ç¤ºä¾‹
2. é¡¹ç›®ä»ç®—å­ naive å®ç°åˆ°ä¼˜åŒ–ç‰ˆæœ¬å‡åŒ…å«å®Œæ•´ä»£ç ï¼Œä¾¿äºè°ƒè¯•ä¸æ€§èƒ½åˆ†æ
3. æ¯ä¸ªç®—å­é™„æœ‰ç›¸å…³çš„ GPU çŸ¥è¯†ç‚¹ï¼Œå¸®åŠ©æ±‚èŒè€…é«˜æ•ˆå¤‡æˆ˜ CUDA ç¼–ç¨‹é¢è¯•

ç›®å‰è¦†ç›–ä»¥ä¸‹ CUDA å¸¸è§ç®—å­åŠå…¶ä¼˜åŒ–ç‰ˆæœ¬ï¼š

|     æ–‡ä»¶å¤¹     |    æè¿°    |                å†…å®¹                 | è€ƒå¯Ÿé¢‘ç‡ |
| :---------: | :------: | :-------------------------------: | :--: |
|   example   | ä¸€äº›ç®€å•çš„ä¾‹å­  |                 /                 |  /   |
| elementwise | æ•°ç»„å¯¹åº”å…ƒç´ è®¡ç®— |                add                |  ä½   |
|    gemv     |  çŸ©é˜µä¹˜å‘é‡   |               sgemv               |  ä½   |
|   reduce    |  å½’çº¦è®¡ç®—ä¼˜åŒ–  | sum, max, softmax, softmax_matrix |  é«˜   |
|    sgemm    |  çŸ©é˜µä¹˜ä¼˜åŒ–   | naive, blocktile, threadtile, ... |  ä¸­   |
|  transpose  |  çŸ©é˜µè½¬ç½®ä¼˜åŒ–  |    naive, ä¼˜åŒ–è®¿å­˜å¹¶è§£å†³bank conflict    |  ä¸­   |


## ç®—å­æ‰‹æ’•è¯´æ˜
é¢è¯•æ—¶ä¸ä¼šæä¾› CUDA è¿è¡Œç¯å¢ƒï¼Œä¹Ÿä¸ä¼šè¦æ±‚å®Œæ•´å†™å‡ºå¯ä»¥è¿è¡Œçš„ä»£ç ï¼Œé€šå¸¸åªéœ€è¦å†™å‡º CUDA ç®—å­å‡½æ•°ï¼ˆå¤§éƒ¨åˆ†æƒ…å†µåªéœ€è¦å†™è¿™ä¸ªï¼‰ï¼Œblock_sizeï¼Œgrid_size å’Œå‡½æ•°è°ƒç”¨ã€‚

åœ¨æ­¤åˆ—å‡ºä¸€äº›å®ï¼Œåé¢ä¼šç”¨åˆ°ï¼š
```cpp
// 1. å‘ä¸Šå–æ•´
#define CEIL(a, b) ((a + b - 1) / (b))

// 2. FLOAT4ï¼Œç”¨äºå‘é‡åŒ–è®¿å­˜ï¼Œä»¥ä¸‹ä¸¤ç§éƒ½å¯ä»¥
// cå†™æ³•
#define FLOAT4(value) *(float4*)(&(value))

// c++å†™æ³•
#define FLOAT4(value) (reinterpret_cast<float4*>(&(value))[0])
```

**æœ¬æ–‡å‰©ä½™ç¯‡å¹…ä»è¿™ä¸€è§’åº¦å‡ºå‘ï¼Œå±•ç¤ºå¿…è¦çš„ä»£ç ï¼Œä»¥ä¾›å‚è€ƒå’Œç»ƒä¹ ã€‚**

# elementwise
**è€ƒå¯Ÿé¢‘ç‡**ï¼š<span style="color: blue; font-weight: bold;">ä½</span>

**ç®—å­æè¿°**ï¼šelementwise æ˜¯æœ€ç®€å•çš„**ä¸€ç±»ç®—å­**ï¼Œå…¶æŒ‡çš„æ˜¯å¯¹æ•°æ®è¿›è¡Œé€å…ƒç´ æ“ä½œï¼Œä¾‹å¦‚å°†ä¸¤ä¸ªç­‰é•¿çš„æ•°ç»„å¯¹åº”å…ƒç´ ç›¸åŠ ï¼ˆ[add](./elementwise/add.cu)ï¼‰ã€‚å¦å¤–åœ¨æ·±åº¦å­¦ä¹ ä¸­ï¼Œæ¿€æ´»å‡½æ•°ä¼šå¯¹è¾“å…¥æ•°æ®çš„æ¯ä¸ªå…ƒç´ æ±‚å¯¹åº”æ¿€æ´»å€¼ï¼Œæ•…æ¿€æ´»å‡½æ•°ä¹Ÿç®—åœ¨ elementwise èŒƒå›´å†…ã€‚

ç®—å­ä¸»è¦åˆ†ä¸¤ç§å†™æ³•ï¼š
1. naiveï¼šæ¯ä¸ªçº¿ç¨‹è´Ÿè´£ä¸€ä¸ªå…ƒç´ çš„è¿ç®—
2. ä½¿ç”¨**float4**ç­‰å‘é‡åŒ–è®¿å­˜æ–¹å¼ï¼šåªå¯¹å¤§è§„æ¨¡æ•°æ®æœ‰åŠ é€Ÿæ•ˆæœï¼Œéœ€è¦æ³¨æ„ï¼Œ**è¦åœ¨ grid ä¸Šé™¤ä»¥ 4**ï¼Œè€Œä¸æ˜¯åœ¨ block ä¸Šé™¤ä»¥ 4ï¼Œå¦åˆ™ä¼šé™ä½SMçš„å ç”¨ç‡ï¼Œå¯ä»¥å‚è€ƒğŸ‘‰[grid_size å’Œ block_size é€‰æ‹©](https://blog.csdn.net/LostUnravel/article/details/135721041)ï¼Œgrid_size ä¸å°äº SMä¸Šæœ€å¤§åŒæ—¶æ‰§è¡Œçš„çº¿ç¨‹æ•°/æœ€å¤§åŒæ—¶æ‰§è¡Œçš„çº¿ç¨‹å—æ•° (Occupancy)ï¼Œå‘é‡åŒ–å­˜å–çš„å¥½å¤„åœ¨äºå¯ä»¥æé«˜å¸¦å®½åˆ©ç”¨ç‡ï¼Œå‡å°‘ç¼“å­˜åˆ©ç”¨ç‡ã€‚

**æºç æ–‡ä»¶å¤¹**ï¼š[./elementwise](./elementwise)

## add

æºç ï¼š[./elementwise/add.cu](./elementwise/add.cu)

### naiveç‰ˆ
```cpp
// block_sizeï¼Œgrid_size å’Œå‡½æ•°è°ƒç”¨
int block_size = 1024;
int grid_size  = CEIL(N, block_size);
elementwise_add<<<grid_size, block_size>>>(a, b, c, N);

// å‡½æ•°å®šä¹‰
__global__ void elementwise_add(float* a, float* b, float *c, int N) {
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    if (idx < N) {
        c[idx] = a[idx] + b[idx];
    }
}
```

### ä½¿ç”¨å‘é‡åŒ–è®¿å­˜
ä½¿ç”¨å‘é‡åŒ–è®¿å­˜è¿›è¡Œä¼˜åŒ–ï¼Œéœ€è¦æ³¨æ„ï¼Œ**è¦åœ¨ grid ä¸Šé™¤ä»¥ 4**ï¼š

```cpp
int block_size = 1024;
int grid_size  = CEIL(CEIL(N,4), block_size);  // æ³¨ï¼šåœ¨gridç»´åº¦é™¤ä»¥4
elementwise_add<<<grid_size, block_size>>>(a, b, c, N);

__global__ void elementwise_add_float4(float* a, float* b, float *c, int N) {
    int idx = (blockDim.x * blockIdx.x + threadIdx.x) * 4;

    if (idx < N) {
        float4 tmp_a = FLOAT4(a[idx]);
        float4 tmp_b = FLOAT4(b[idx]);
        float4 tmp_c;
        tmp_c.x = tmp_a.x + tmp_b.x;
        tmp_c.y = tmp_a.y + tmp_b.y;
        tmp_c.z = tmp_a.z + tmp_b.z;
        tmp_c.w = tmp_a.w + tmp_b.w;
        FLOAT4(c[idx]) = tmp_c;
    }
}
```

ä»¥ä¸‹ç®—å­çš„ block_size, grid_size, å‡½æ•°è°ƒç”¨ä¸ add çš„å†™æ³•ç›¸åŒ, ä¸å†é‡å¤å†™å‡ºã€‚

## sigmoid

$$\sigma(x) = \frac{1}{1 + e^{-x}} $$

```cpp
__global__ void sigmoid(float* x, float* y, int N) {
    int idx = blockIdx.x * blockDim.x + threadIdx.x;
    if (idx < N) y[idx] = 1.0f / (1.0f + expf(-x[idx]));
}

// float4
__global__ void sigmoid_float4(float* x, float* y, int N) {
    int idx = (blockDim.x * blockIdx.x + threadIdx.x) * 4;
    if (idx < N) {
        float4 tmp_x = FLOAT4(x[idx]);
        float4 tmp_y;
        tmp_y.x = 1.0f / (1.0f + expf(-tmp_x.x));
        tmp_y.y = 1.0f / (1.0f + expf(-tmp_x.y));
        tmp_y.z = 1.0f / (1.0f + expf(-tmp_x.z));
        tmp_y.w = 1.0f / (1.0f + expf(-tmp_x.w));
        FLOAT4(y[idx]) = tmp_y;
    }
}
```

## relu

$$ \text{ReLU}(x) = \max(0, x) $$

```cpp
__global__ void relu(float* x, float* y, int N) {
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    if (idx < N) y[idx] = fmaxf(0.0f, x[idx]);
    }

// float4
__global__ void relu_float4(float* x, float* y, int N) {
    int idx = (blockDim.x * blockIdx.x + threadIdx.x) * 4;
    if (idx < N) {
        float4 tmp_x = FLOAT4(x[idx]);
        float4 tmp_y;
        tmp_y.x = fmaxf(0.0f, tmp_x.x);
        tmp_y.y = fmaxf(0.0f, tmp_x.y);
        tmp_y.z = fmaxf(0.0f, tmp_x.z);
        tmp_y.w = fmaxf(0.0f, tmp_x.w);
        FLOAT4(y[idx]) = tmp_y;
    }
}
```

# reduce
**è€ƒå¯Ÿé¢‘ç‡**ï¼š<span style="color: red; font-weight: bold;">é«˜</span>

**ç®—å­æè¿°**ï¼šreduce æ˜¯ä¸€ç§èšåˆæ“ä½œï¼Œé€šå¸¸ç”¨äºå°†ä¸€ä¸ªå¤šå…ƒç´ çš„æ•°æ®ç»“æ„ï¼ˆå¦‚æ•°ç»„æˆ–å¼ é‡ï¼‰é€šè¿‡æŸç§è§„åˆ™å½’çº¦ä¸ºä¸€ä¸ªæ›´å°çš„æ•°æ®ç»“æ„ï¼ˆé€šå¸¸æ˜¯å•ä¸ªå€¼æˆ–æ›´å°çš„æ•°ç»„ï¼‰ã€‚å®ƒå¹¿æ³›åº”ç”¨äºæ•°æ®å¤„ç†ã€å¹¶è¡Œè®¡ç®—ä»¥åŠæ·±åº¦å­¦ä¹ ä¸­ã€‚ä¾‹å¦‚å¯¹æ•°ç»„è¿›è¡Œæ±‚å’Œ (sum)ï¼Œæ±‚å‡å€¼ (mean)ï¼Œæ±‚æœ€å¤§å€¼ (max)ï¼Œè¿˜æœ‰æ±‚ softmaxã€‚å…¶ä¸­ï¼Œ**sum å’Œ softmax çš„è€ƒå¯Ÿé¢‘ç‡æœ€é«˜**ã€‚

**æºç æ–‡ä»¶å¤¹**ï¼š[./reduce](./reduce)

## sum

æºç ï¼š[./reduce/sum/sum.cu](./reduce/sum/sum.cu)

### naiveç‰ˆ

æ¯ä¸ªçº¿ç¨‹é€šè¿‡åŸå­å‡½æ•° `atomicAdd`ï¼Œå¾€åŒä¸€ä¸ªå…¨å±€å†…å­˜é‡Œé¢å†™æ•°æ®ï¼ŒåŸå­å‡½æ•°ä¼šå¯¼è‡´çº¿ç¨‹å˜æˆåºåˆ—åŒ–ï¼Œä¸§å¤±å¹¶è¡Œæ€§ï¼Œç®—å­æ€§èƒ½å¤§å¤§é™ä½ï¼Œä¸èƒ½æ»¥ç”¨ï¼š

```cpp
dim3 block_size(BLOCK_SIZE);  // BLOCK_SIZE æ˜¯é€šè¿‡å®å®šä¹‰çš„æŸä¸ªæ•°å­—
dim3 grid_size(CIEL(N, BLOCK_SIZE));
reduce_v1<<<grid_size, block_size>>>(d_x, d_y, N);

__global__ void reduce_v1(const float* input, float* output, int N) {
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    if (idx < N) atomicAdd(output, input[idx]);
}
```

### æŠ˜åŠå½’çº¦

åœ¨blockå†…è¿›è¡ŒæŠ˜åŠå½’çº¦ï¼Œä¸€ä¸ªblockå½’çº¦ä¸€éƒ¨åˆ†ï¼Œå…ˆæ¬åˆ°è‡ªå·± block å†…çš„ shared_memory ä¸‹ï¼Œç„¶åå½’çº¦åˆ°é¦–å…ƒç´ ã€‚

> è¿™ç§æ–¹æ³•çš„ç¼ºç‚¹æ˜¯ BLOCK_SIZE å¿…é¡»æ˜¯ 2 çš„å¹‚æ¬¡ï¼Œå¦åˆ™æŠ˜åŠæ“ä½œæ—¶ä¼šè®¡ç®—å‡ºé”™ï¼Œå¯¼è‡´è¯¯å·®å¾ˆå¤§ã€‚è€Œä¸”æ¯æ¬¡è¿­ä»£æŠ˜åŠæ—¶å¿…é¡»ä½¿ç”¨ `__syncthreads()` è¿›è¡ŒåŒæ­¥ï¼Œä¼šå¼ºåˆ¶æ‰€æœ‰çº¿ç¨‹åœ¨åŒæ­¥ç‚¹ç­‰å¾…ï¼Œç›´åˆ°çº¿ç¨‹å—ä¸­çš„å…¶ä»–çº¿ç¨‹ä¹Ÿåˆ°è¾¾ã€‚ä¼šå¯¼è‡´æ€§èƒ½ä¸‹é™ã€‚

```cpp
dim3 block_size(BLOCK_SIZE);  // BLOCK_SIZE æ˜¯é€šè¿‡å®å®šä¹‰çš„æŸä¸ªæ•°å­—
dim3 grid_size(CIEL(N, BLOCK_SIZE));
reduce_v2<<<grid_size, block_size>>>(d_x, d_y, N);

__global__ void reduce_v2(const float* input, float* output, int N) {
    int tid = threadIdx.x;
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    __shared__ float input_s[BLOCK_SIZE];

    // 1. æ¬è¿å’Œçº¿ç¨‹æ•°é‡(blockDim.x)ç›¸ç­‰çš„æ•°æ®ï¼Œåˆ°å½“å‰blockçš„å…±äº«å†…å­˜ä¸­
    input_s[tid] = (idx < N) ? input[idx] : 0.0f;
    __syncthreads();

    // 2. ç”¨1/2, 1/4, 1/8...çš„çº¿ç¨‹è¿›è¡ŒæŠ˜åŠå½’çº¦
    for (int offset = blockDim.x >> 1; offset > 0; offset >>= 1) {
        if (tid < offset) {  // 2.æŠ˜åŠå½’çº¦
            input_s[tid] += input_s[tid + offset];
        }
        __syncthreads();
    }

    // 3. æ¯ä¸ªblockçš„ç¬¬ä¸€ä¸ªçº¿ç¨‹å°†è®¡ç®—ç»“æœç´¯åŠ åˆ°è¾“å‡ºä¸­
    if (tid == 0) atomicAdd(output, input_s[0]);
}
```

### warp shuffleï¼ˆæ¨èå†™æ³•ï¼‰

åœ¨ warp å†…è¿›è¡ŒæŠ˜åŠå½’çº¦ï¼Œå…¶ä¼˜åŠ¿åœ¨äºï¼Œä¸€ä¸ª warp å†…çš„çº¿ç¨‹æ˜¯åŒæ­¥çš„ï¼Œç›¸æ¯”äºä»¥ block ä¸ºå•ä½è¿›è¡ŒæŠ˜åŠï¼Œä»¥ warp ä¸ºå•ä½è¿›è¡Œæ¯æ¬¡æŠ˜åŠæ—¶ä¸éœ€è¦ `__syncthreads()`ï¼Œå¹¶è¡Œæ€§æ›´é«˜ã€‚ 

> BLOCK_SIZEéœ€è¦æ˜¯32çš„æ•´æ•°å€ï¼Œå¦åˆ™äº§ç”Ÿçº¿ç¨‹æ•°ä¸è¶³32çš„warpï¼Œå¯èƒ½ä¼šå¯¼è‡´è®¿é—®åˆ°æ— æ•ˆæ•°æ®ã€‚

**ä½¿ç”¨ CUDA æä¾›çš„ warp shuffle æ“ä½œ**ï¼Œæœ‰ä»¥ä¸‹å‡½æ•°å¯ä»¥ç”¨ï¼š

1. `__shfl_sync()`ï¼šæ‹·è´æ¥è‡ªä»»æ„laneId(0~31)çº¿ç¨‹é‡Œçš„å€¼
2. `__shf_xor_sync()`ï¼šæ‹·è´æ¥è‡ªä¸€ä¸ªè®¡ç®—å‡ºæ¥çš„laneId(0~31)çº¿ç¨‹é‡Œçš„å€¼
3. `__shfl_up_sync()`ï¼šæ‹·è´æ¥è‡ªæœ‰ä¸€å®šåç§»é‡laneIdæ›´å°çš„çº¿ç¨‹é‡Œçš„å€¼
4. `__sync_down_sync()`ï¼šæ‹·è´æ¥è‡ªæœ‰ä¸€å®šåç§»é‡laneIdæ›´å¤§çš„çº¿ç¨‹é‡Œçš„å€¼

å…¶ä¸­ `__shf_xor_sync()` å’Œ `__sync_down_sync()` ä½¿ç”¨é¢‘ç‡è¾ƒé«˜ã€‚

```cpp
dim3 block_size(BLOCK_SIZE);
dim3 grid_size(CIEL(N, BLOCK_SIZE));
reduce_v3<<<grid_size, block_size>>>(d_x, d_y, N)

__global__ void reduce_v3(float* d_x, float* d_y, const int N) {
    __shared__ float s_y[32];  // ä»…éœ€è¦32ä¸ªï¼Œå› ä¸ºä¸€ä¸ªblockæœ€å¤š1024ä¸ªçº¿ç¨‹ï¼Œæœ€å¤š1024/32=32ä¸ªwarp

    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    int warpId = threadIdx.x / warpSize;  // å½“å‰çº¿ç¨‹å±äºå“ªä¸ªwarp
    int laneId = threadIdx.x % warpSize;  // å½“å‰çº¿ç¨‹æ˜¯warpä¸­çš„ç¬¬å‡ ä¸ªçº¿ç¨‹

    float val = (idx < N) ? d_x[idx] : 0.0f;  // æ¬è¿d_x[idx]åˆ°å½“å‰çº¿ç¨‹çš„å¯„å­˜å™¨ä¸­
    #pragma unroll
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        val += __shfl_down_sync(0xFFFFFFFF, val, offset);   // åœ¨ä¸€ä¸ªwarpé‡ŒæŠ˜åŠå½’çº¦
    }

    if (laneId == 0) s_y[warpId] = val;  // æ¯ä¸ªwarpé‡Œçš„ç¬¬ä¸€ä¸ªçº¿ç¨‹ï¼Œè´Ÿè´£å°†æ•°æ®å­˜å‚¨åˆ°shared memä¸­
    __syncthreads();

    if (warpId == 0) {  // ä½¿ç”¨æ¯ä¸ªblockä¸­çš„ç¬¬ä¸€ä¸ªwarpå¯¹s_yè¿›è¡Œæœ€åçš„å½’çº¦
        int warpNum = blockDim.x / warpSize;  // æ¯ä¸ªblockä¸­çš„warpæ•°é‡
        val = (laneId < warpNum) ? s_y[laneId] : 0.0f;
        for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
            val += __shfl_down_sync(0xFFFFFFFF, val, offset);
        }
        if (laneId == 0) atomicAdd(d_y, val);  // ä½¿ç”¨æ­¤warpä¸­çš„ç¬¬ä¸€ä¸ªçº¿ç¨‹ï¼Œå°†ç»“æœç´¯åŠ åˆ°è¾“å‡º
    }
}
```

### warp shuffle + float4
åœ¨ warp shuffle ä¸Šè¿›ä¸€æ­¥ä¼˜åŒ–ï¼Œæ¬è¿æ•°æ®æ—¶ä½¿ç”¨ float4ï¼š

```cpp
#define FLOAT4(value) (float4*)(&(value))[0]
dim3 block_size(BLOCK_SIZE);
dim3 grid_size(CEIL(CIEL(N, BLOCK_SIZE),4));  // è¿™é‡Œè¦é™¤ä»¥4
reduce_v3<<<grid_size, block_size>>>(d_x, d_y, N)

__global__ void reduce_v4(float* d_x, float* d_y, const int N) {
    __shared__ float s_y[32];
    int idx = (blockDim.x * blockIdx.x + threadIdx.x) * 4;  // è¿™é‡Œè¦ä¹˜ä»¥4
    int warpId = threadIdx.x / warpSize;   // å½“å‰çº¿ç¨‹ä½äºç¬¬å‡ ä¸ªwarp
    int laneId = threadIdx.x % warpSize;   // å½“å‰çº¿ç¨‹æ˜¯warpä¸­çš„ç¬¬å‡ ä¸ªçº¿ç¨‹
    float val = 0.0f;
    if (idx < N) {
        float4 tmp_x = FLOAT4(d_x[idx]);
        val += tmp_x.x;
        val += tmp_x.y;
        val += tmp_x.z;
        val += tmp_x.w;
    }
    #pragma unroll
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        val += __shfl_down_sync(0xFFFFFFFF, val, offset);
    }

    if (laneId == 0) s_y[warpId] = val;
    __syncthreads();

    if (warpId == 0) {
        int warpNum = blockDim.x / warpSize;
        val = (laneId < warpNum) ? s_y[laneId] : 0.0f;
        for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
            val += __shfl_down_sync(0xFFFFFFFF, val, offset);
        }
        if (landId == 0) atomicAdd(d_y, val);
    }
}
```

## SoftMax

Softmax çš„ CPU å’Œ CUDA å†™æ³•å‡æ˜¯é«˜é¢‘è€ƒå¯Ÿã€‚é¢è¯•æ—¶æœ‰å¯èƒ½ä¼šè®©ä»»é€‰ä¸€ç§å†™æ³•è¿›è¡Œä¹¦å†™ï¼Œæ­¤æ—¶è‡ªå·±å¯ä»¥é‡åŠ›è€Œè¡Œã€‚

æºç ï¼š[./reduce/softmax/softmax.cu](./reduce/softmax/softmax.cu)

Softmaxå…¬å¼å¦‚ä¸‹ï¼š

$$
\text{Softmax}(x_i) = \frac{e^{x_i}}{\sum_{j=1}^{N} e^{x_j}}
$$

ä¸€èˆ¬ä¸ºäº†é¿å…æº¢å‡ºï¼Œéœ€è¦å‡å»æœ€å¤§å€¼ï¼Œæ‰€ä»¥é€šå¸¸é‡‡ç”¨ä¸‹é¢è¿™ä¸ªå…¬å¼ï¼š

$$
\text{Softmax}(x_i) = \frac{e^{x_i-M}}{\sum_{j=1}^{N} (e^{x_j-M})}
$$

å…¶ä¸­ $M$ æ˜¯è¾“å…¥å‘é‡çš„æœ€å¤§å€¼ã€‚

### CPU å†™æ³•
```cpp
void softmax(float* input, float* output, int N) {
    int M = *(std::max_element(input, input + N));
    float div = 0;
    for (int i = 0; i < N; i++) {
        output[i] = std::exp(input[i] - M);
        div += output[i];
    }
    for (int i = 0; i < N; i++) {
        output[i] /= div;
    }
}
```

### CUDAå†™æ³•

æœ€ç›´æ¥çš„æ€è·¯æ˜¯å°† Softmax è®¡ç®—è¿‡ç¨‹æ‹†åˆ†ä¸ºå¤šä¸ªå½’çº¦ç®—å­ï¼Œåªè¦ä¼šå†™å½’çº¦ï¼Œé‚£ä¹ˆ Softmax å°±èƒ½å†™ã€‚

è¿™ç§å†™æ³•çš„ä¼˜ç‚¹æ˜¯æ¯”è¾ƒç®€å•ï¼Œè™½ç„¶ä»£ç æ¯”è¾ƒå¤šï¼Œä½†åŸºæœ¬éƒ½æ˜¯é‡‡ç”¨å½’çº¦çš„å†™æ³•ï¼Œå‡ ä¸ªç®—å­çš„é€»è¾‘ä¸Šå·®å¼‚ä¸å¤§ã€‚ç¼ºç‚¹æ˜¯ç®—å­æ•ˆç‡æ¯”è¾ƒä½ã€‚**è¿™é‡Œå»ºè®®å­¦ä¹  [softmax_matrix](#softmax_matrix) çš„å†™æ³•ï¼**

æ€è·¯ï¼š
- æ ¸å‡½æ•°1ï¼šå½’çº¦æ±‚æœ€å€¼ max_val
- æ ¸å‡½æ•°2ï¼šå½’çº¦æ±‚å’Œ sum
- æ ¸å‡½æ•°3ï¼šè®¡ç®—æ¯ä¸ªå…ƒç´ å‡å» max_val é™¤ä»¥ sumã€‚

```cpp
__device__ static float atomicMax(float* address, float val) {
    int* address_as_i = (int*)address;
    int old = *address_as_i;
    int assumed;
    do {
        assumed = old;
        old = atomicCAS(address_as_i, assumed, __float_as_int(fmaxf(val, __int_as_float(assumed))));
    } while (assumed != old);
    return __int_as_float(old);
}

__global__ void max_kernel(float* input, float* max_val, int N) {
    __shared__ float s_mem[32];
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    int warpId = threadIdx.x / warpSize;
    int laneId = threadIdx.x % warpSize;

    float val = (idx < N) ? input[idx] : (-FLT_MAX);
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        val = fmaxf(val, __shfl_down_sync(0xFFFFFFFF, val, offset));
    }
    if (laneId == 0) s_mem[warpId] = val;
    __syncthreads();

    if (warpId == 0) {
        int warpNum = blockDim.x / warpSize;
        val = (laneId < warpNum) ? s_mem[laneId] : (-FLT_MAX);
        for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
            val = fmaxf(val, __shfl_down_sync(0xFFFFFFFF, val, offset));
        }
        if (laneId == 0) atomicMax(max_val, val);
    }
}

__global__ void sum_kernel(float* input, float* sum, float* max_val, int N) {
    __shared__ float s_mem[32];
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    int warpId = threadIdx.x / warpSize;
    int laneId = threadIdx.x % warpSize;

    float val = (idx < N) ? expf(input[idx] - *max_val) : 0.0f;
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        val += __shfl_down_sync(0xFFFFFFFF, val, offset);
    }
    if (laneId == 0) s_mem[warpId] = val;
    __syncthreads();

    if (warpId == 0) {
        int warpNum = blockDim.x / warpSize;
        val = (laneId < warpNum) ? s_mem[laneId] : 0.0f;
        for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
            val += __shfl_down_sync(0xFFFFFFFF, val, offset);
        }
        if (laneId == 0) atomicAdd(sum, val);
    }
}

__global__ void softmax_kernel(float* input, float* output, float* sum, float* max_val, int N) {
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    if (idx < N) output[idx] = expf(input[idx] - *max_val) / (*sum);
}

// åˆå§‹åŒ–ç›¸å…³å˜é‡
// ...
// è°ƒç”¨
int block_size = 256;
int grid_size  = CEIL(N, block_size);
max_kernel<<<gird_size, block_size>>>(input, max_val, N);
sum_kernel<<<gird_size, block_size>>>(input, sum, max_val, N);
softmax_kernel<<<gird_size, block_size>>>(input, output, sum, max_val, N);
```

# transpose
**è€ƒå¯Ÿé¢‘ç‡**ï¼š<span style="color: red; font-weight: bold;">ä¸­</span>

**ç®—å­æè¿°**ï¼šæŒ‡çš„æ˜¯çŸ©é˜µè½¬ç½®ï¼Œå…¶ä¸­ä¼šæ¶‰åŠåˆ° GPU å…¨å±€å†…å­˜çš„é«˜æ•ˆè®¿é—®ã€bank conflict çŸ¥è¯†ç‚¹ã€‚

å¦‚ä½•ä¼˜åŒ–å…¨å±€å†…å­˜çš„è®¿é—®ï¼š
1. **å°½é‡åˆå¹¶è®¿é—®**ï¼Œå³è¿ç»­çš„çº¿ç¨‹è¯»å–è¿ç»­çš„å†…å­˜ï¼Œä¸”å°½é‡è®©è®¿é—®çš„å…¨å±€å†…å­˜çš„é¦–åœ°å€æ˜¯32å­—èŠ‚ï¼ˆä¸€æ¬¡æ•°æ®ä¼ è¾“å¤„ç†çš„æ•°æ®é‡ï¼‰çš„å€æ•°ï¼ˆcudaMallocåˆ†é…çš„è‡³å°‘æ˜¯256å­—èŠ‚æ•´æ•°å€ï¼‰ï¼›
2. å¦‚æœä¸èƒ½åŒæ—¶åˆå¹¶è¯»å–å’Œå†™å…¥ï¼Œåˆ™åº”è¯¥**å°½é‡åšåˆ°åˆå¹¶å†™å…¥**ï¼Œå› ä¸ºç¼–è¯‘å™¨å¦‚æœèƒ½åˆ¤æ–­ä¸€ä¸ªå…¨å±€å†…å­˜å˜é‡åœ¨æ ¸å‡½æ•°å†…æ˜¯åªå¯è¯»çš„ï¼Œä¼šè‡ªåŠ¨è°ƒç”¨ `__ldg()` è¯»å–å…¨å±€å†…å­˜ï¼Œä»è€Œå¯¹æ•°æ®è¿›è¡Œç¼“å­˜ï¼Œç¼“è§£éåˆå¹¶è®¿é—®å¸¦æ¥çš„å½±å“ï¼Œä½†è¿™åªå¯¹è¯»å–æœ‰æ•ˆï¼Œå†™å…¥åˆ™æ²¡æœ‰ç±»ä¼¼çš„å‡½æ•°ã€‚å¦å¤–ï¼Œå¯¹äºå¼€æ™®å‹’æ¶æ„å’Œéº¦å…‹æ–¯éŸ¦æ¶æ„ï¼Œéœ€è¦æ˜¾å¼çš„ä½¿ç”¨ `__ldg()` å‡½æ•°ï¼Œä¾‹å¦‚ `B[ny * N + nx] = __ldg(&A[nx * N + ny])`ã€‚

**æºç æ–‡ä»¶å¤¹**ï¼š[./transpose](./transpose)

naiveï¼š
```cpp
__global__ void transpose(float* input, float* output, int M, int N) {
    // inputçš„rowå’Œcol
    int row = blockDim.y * blockIdx.y + threadIdx.y;
    int col = blockDim.x * blockIdx.x + threadIdx.x;

    if (row < M && col < N) {
        output[col * M + row] = input[row * N + col];
    }
}
```

ä»…åˆå¹¶å†™å…¥ï¼š
```cpp
__global__ void transpose(float* input, float* output, int M, int N) {
    // outputçš„rowå’Œcol
    int row = blockDim.y * blockIdx.y + threadIdx.y;
    int col = blockDim.x * blockIdx.x + threadIdx.x;

    if (row < N && col < M) {
        output[row * M + col] = __ldg(&input[col * N + row]);  // åˆå¹¶å†™å…¥ï¼Œè¯»å–ä½¿ç”¨__ldgè¿›è¡Œç¼“å­˜
    }
}
```

ä½¿ç”¨å…±äº«å†…å­˜ä¸­è½¬ï¼ŒåŒæ—¶åˆå¹¶è¯»å–å’Œå†™å…¥ï¼ˆ**æ¨è**ï¼‰ï¼š
![shareMem](./transpose/assets/sharedMem.png)

```cpp
// è¾“å…¥çŸ©é˜µæ˜¯Mè¡ŒNåˆ—ï¼Œè¾“å‡ºçŸ©é˜µæ˜¯Nè¡ŒMåˆ—
dim3 block(32, 32);
dim3 grid(CEIL(M,32), CEIL(N,32));

template <const int BLOCK_SIZE>
__global__ void transpose(float* input, float* output, int M, int N) {
    __shared__ float s_mem[BLOCK_SIZE][BLOCK_SIZE + 1];  // é¿å…bank conflict
    int bx = blockIdx.x * BLOCK_SIZE;
    int by = blockIdx.y * BLOCK_SIZE;
    int x1 = bx + threadIdx.x;
    int y1 = by + threadIdx.y;

    if (x1 < N && y1 < M) {
        s_mem[threadIdx.y][threadIdx.x] = input[y1 * N + x1];
    }
    __syncthreads();

    int x2 = by + threadIdx.x;
    int y2 = bx + threadIdx.y;
    if (x2 < M && y2 < N) {
        output[y2 * M + x2] = s_mem[threadIdx.x][threadIdx.y];  // paddingåï¼Œä¸å­˜åœ¨bank conflict
    }
}
```

# sgemm
**è€ƒå¯Ÿé¢‘ç‡**ï¼š<span style="color: red; font-weight: bold;">ä¸­</span>

**ç®—å­æè¿°**ï¼šæŒ‡çš„æ˜¯çŸ©é˜µä¹˜ã€‚çŸ©é˜µä¹˜æ˜¯ CUDA å­¦ä¹ æ—¶çš„ç»å…¸æ¡ˆä¾‹ï¼Œæ¶‰åŠå¤šç§ CUDA ç¼–ç¨‹ä¸­çš„å¸¸ç”¨ä¼˜åŒ–æŠ€å·§ã€‚å»ºè®®é˜…è¯» [./sgemm/README.md](./sgemm/README.md)ã€‚ä½†æ‰‹æ’•æ—¶éš¾åº¦å¾€å¾€è¾ƒå¤§ï¼Œå»ºè®®ä¼˜å…ˆæŒæ¡æœ€ç®€å•çš„ naive ç‰ˆæœ¬ä»¥åŠ block_tile ç‰ˆæœ¬ã€‚æŒæ¡ block_tile ç‰ˆæœ¬åï¼Œåªéœ€è¦åŠ ä¸€äº›ä»£ç å°±å¯ä»¥ä¼˜åŒ–ä¸º thread_tile ç‰ˆæœ¬ï¼Œæ•…ä¹Ÿå¯ä»¥è€ƒè™‘æŒæ¡ã€‚å…¶ä½™çš„æ›´é«˜æ•ˆçš„ä¼˜åŒ–ç‰ˆæœ¬ï¼Œä¸ªäººè®¤ä¸ºäº†è§£å…¶åŸç†å³å¯ï¼Œä¸å¿…å¼ºæ±‚é¢è¯•æ—¶æ‰‹å†™ã€‚

**æºç æ–‡ä»¶å¤¹**ï¼š[./sgemm](./sgemm)

## naive ç‰ˆ
```cpp
// C(MxN) = A(MxK) * B(KxN) è¡Œä¼˜å…ˆ
// æ¯ä¸ªçº¿ç¨‹å¤„ç†ä¸€ä¸ªè¾“å‡ºçŸ©é˜µä¸­çš„å…ƒç´ 

// å‡è®¾ M N K å·²ç»èµ‹å€¼
const int BLOCK_SIZE = 32;
dim3 block(BLOCK_SIZE, BLOCK_SIZE);
dim3 grid((M+BLOCK_SIZE-1)/BLOCK_SIZE, (N+BLOCK_SIZE-1)/BLOCK_SIZE);
sgemm<<<grid, block>>>(d_A, d_B, d_C, M, N, K);

__global__ void sgemm(float* A, float* B, float* C, int M, int N, int K) {
    int col = blockDim.x * blockIdx.x + threadIdx.x;
    int row = blockDim.y * blockIdx.y + threadIdx.y;
    if (row >= M || col >= N) return;

    float accum = 0.0f;
    for (int i = 0; i < K; i++) {
        accum += A[row * K + i] * B[i * N + col];
    }

    C[row * N + col] = accum;
}
```
## block_tile ç‰ˆæœ¬
è¿˜æ˜¯ä¸€ä¸ªçº¿ç¨‹è®¡ç®—ä¸€ä¸ªè¾“å‡ºçŸ©é˜µä¸­çš„å…ƒç´ ï¼Œä½†æ˜¯ç”¨ shared mem åšç¼“å­˜ï¼Œé‡å¤ä» shared mem ä¸­è¯»å–ï¼Œè€Œä¸æ˜¯ä» global memï¼Œè™½ç„¶è¯»å–æ¬¡æ•°æ²¡å˜å°‘ï¼Œä½†æ˜¯ shared mem æ¯” global mem è¯»å–é€Ÿåº¦å¿«ï¼š

```cpp
#define BLOCK_SIZE 32

dim3 block(BLOCK_SIZE, BLOCK_SIZE);
dim3 grid(CEIL(M,BLOCK_SIZE), CEIL(N,BLOCK_SIZE));
sgemm<<<grid, block>>>(d_A, d_B, d_C, M, N, K);

__global__ void sgemm(float* A, float* B, float* C, int M, int N, int K) {
    int idx = blockDim.x * blockIdx.x + threadIdx.x;
    int idy = blockDim.y * blockIdx.y + threadIdx.y;
    if (idx >= M || idy >= N) return;

    int bx = blockIdx.x;
    int by = blockIdx.y;
    int tx = threadIdx.x;
    int ty = threadIdx.y;

    const int BM = BLOCK_SIZE;
    const int BN = BLOCK_SIZE;
    const int BK = BLOCK_SIZE;
    __shared__ float As[BM * BK];
    __shared__ float Bs[BK * BN];

    // åˆå§‹åŒ–block tileèµ·å§‹ä½ç½®
    A = &A[(by * BM) * K];
    B = &B[bx * BN];
    C = &C[(by * BM) * N + bx * BN];

    float accum = 0.0f;
    for (int k = 0; k < K; k += BK) {
        // æ¬è¿ global ==> shared
        As[ty * BK + tx] = A[ty * K + tx];
        Bs[ty * BN + tx] = B[ty * N + tx];
        __syncthreads();
        A = A + BK;
        B = B + BK * N;
        for (int i = 0; i < BK; i++) {
            accum += As[ty * BK + i] * Bs[i * BN + tx];
        }
        __syncthreads();
    }

    C[ty * N + tx] = accum;
}
```

## thread_tile

ä¸€ä¸ªçº¿ç¨‹æ‰¿æ‹…æ›´å¤šçš„è®¡ç®—ï¼Œæ›´åŠ é«˜æ•ˆï¼š

```cpp
dim3 block(256);
dim3 grid(CEIL(M,128), CEIL(N,128));
sgemm<128, 128, 8, 8, 8><<<grid, block>>>(A,B,C,M,N,K);

template<const int BM,
         const int BN,
         const int BK,
         const int TM,
         const int TN>
__global__ void sgemm(float* A, float* B, float* C, int M, int N, int K) {
    int bx = blockIdx.x;
    int by = blockIdy.y;

    int block_row_thread = BN / TN;  // blockä¸­ä¸€è¡Œçš„threadæ•°é‡
    int block_col_thread = BM / TM;  // blockä¸­ä¸€åˆ—çš„threadæ•°é‡
    int thread_num = block_row_thread * block_col_thread;  // blockä¸­threadæ€»é‡

    int tx = (threadIdx.x % block_row_thread) * TN;  // threadtileå·¦ä¸Šè§’xåæ ‡
    int ty = (threadIdx.x / block_row_thread) * TM;  // threadtileå·¦ä¸Šè§’yåæ ‡

    __shared__ float As[BM * BK];
    __shared__ float Bs[BK * BN];

    A = &A[by * BM * K];
    B = &B[bx * BN];
    C = &C[by * BM * N + bx * BN];

    int a_tile_row = threadIdx.x / BK;
    int a_tile_col = threadIdx.x % BK;
    int a_tile_stride = thread_num / BK;  // BM/(BM/(thread_num/BK)) = thread_num/BK = stride

    int b_tile_row = threadIdx.x / BN;
    int b_tile_col = threadIdx.x % BN;
    int b_tile_stride = thread_num / BN;

    float accum[TM][TN] = {0.0f};
    for (int k = 0; k < K; k += BK) {
        for (int i = 0; i < BM; i += a_tile_stride) {
            As[(a_tile_row + i) * BK + a_tile_col] = A[(a_tile_row + i) * K + a_tile_col];
        }
        for (int i = 0; i < BK; i += b_tile_stride) {
            Bs[(b_tile_row + i) * BN + b_tile_col] = B[(b_tile_row + i) * N + b_tile_col];
        }
        __syncthreads();

        A += BK;
        B += BK * N;

        for (int row = 0; row < TM; row++) {
            for (int col = 0; col < TN; col++) {
                for (int i = 0; i < BK; i++) {
                    accum[row][col] += As[(ty + row) * BK + i] * Bs[i * BN + (tx + col)];
                }
            }
        }
        __syncthreads();
    }
    for (int row = 0; row < TM; row++) {
        for (int col = 0; col < TN; col++) {
            C[(ty + row) * N + (tx + col)] = accum[row][col];
        }
    }
}
```

# gemv
**è€ƒå¯Ÿé¢‘ç‡**ï¼š<span style="color: blue; font-weight: bold;">ä½</span>

**ç®—å­æè¿°**ï¼šæ±‚ä¸€ä¸ªçŸ©é˜µä¹˜ä»¥ä¸€ä¸ªå‘é‡ï¼Œæ–¹æ³•æ˜¯æ¯ä¸ªblockä¸­æœ‰ä¸€ä¸ªwarpï¼Œæ¯ä¸ªwarpè´Ÿè´£ä¸€è¡Œçš„è®¡ç®—ã€‚è™½ç„¶é¢è¯•è€ƒå¯Ÿé¢‘ç‡ä¸å¤§ä½†ï¼Œæ¨èå­¦ä¹ å¹¶äº†è§£ã€‚å› ä¸º gemv ä¸­ä½¿ç”¨ä¸€ä¸ª warp è´Ÿè´£ä¸€è¡Œçš„è®¡ç®—æ–¹å¼ï¼Œå¯ä»¥æ‹“å±•åˆ°å¯¹ä¸€ä¸ªçŸ©é˜µæŒ‰è¡Œæ±‚å½’çº¦ï¼ˆ**é¢è¯•æ—¶æœ‰æ¦‚ç‡ä¼šè€ƒå¯ŸäºŒç»´çŸ©é˜µçš„æŒ‰è¡Œæ±‚å½’çº¦ï¼Œè€Œä¸åªæ˜¯ä¸€ç»´æ•°ç»„**ï¼‰

**æºç æ–‡ä»¶å¤¹**ï¼š[./gemv](./gemv)

## gemv
```cpp
// è¡Œæ•°: M = 1024
// åˆ—æ•°: K = 32
// blockæ•°é‡å’Œè¡Œæ•°ç›¸åŒ: grid_size = M
// æ¯ä¸ªblocké‡Œä¸€ä¸ªwarp: block_size = 32
sgemv<<<grid_size, block_size>>>(A, x, y, M, K);
__global__ void sgemv(float* A, float* x, float* y, int M, int K) {
    int laneId = threadIdx.x % warpSize;
    int row = blockIdx.x;  // 0~M-1
    if (row >= M) return;

    float res = 0.0f;
    int kIteration = CEIL(K, warpSize);  // æ¯ä¸ªçº¿ç¨‹éœ€è¦è´Ÿè´£è®¡ç®—çš„æ•°æ®ä¸ªæ•°

    for (int i = 0; i < kIteration; i++){
        int col = i * warpSize + laneId;
        res += (col < K) ? A[row * K + col] * x[col] : 0.0f;
    }

    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        res += __shfl_down_sync(0xFFFFFFFF, res, offset);
    }

    if(laneId == 0) y[row] = res;
}
```

## æ‹“å±•åº”ç”¨

äº†è§£äº† gemv åï¼ŒæŒ‰ç…§åŒæ ·çš„æ€è·¯ï¼Œæˆ‘ä»¬å¯ä»¥å†™å‡ºå¯¹ MxN çš„çŸ©é˜µï¼Œæ¯ä¸€è¡Œæ±‚ softmaxã€‚M = 1 æ—¶ï¼Œé—®é¢˜å˜ä¸ºå¯¹ä¸€ä¸ªé•¿åº¦ä¸º N çš„æ•°ç»„æ±‚ softmaxã€‚

### softmax_matrix

æºç ï¼š[./reduce/softmax_matrix/softmax_matrix.cu](./reduce/softmax_matrix/softmax_matrix.cu)

å¯¹ä¸€ä¸ª MxN çš„çŸ©é˜µï¼Œæ¯ä¸€è¡Œæ±‚ softmaxï¼Œæ€è·¯åŒæ ·æ˜¯æ¯ä¸ª warp å¤„ç†ä¸€è¡Œï¼Œç”¨è¿™ä¸ª warp å¯¹ä¸€è¡Œè¿›è¡Œæ±‚å’Œã€æ±‚æœ€å€¼ï¼Œè®¡ç®—ç»“æœå­˜å…¥å…±äº«å†…å­˜ï¼Œç„¶åæ¯ä¸ªå…ƒç´ æ±‚ softmaxï¼š
```cpp
__global__ void softmax_kernel(float* input, float* output, int M, int N) {
    __shared__ float s_max_val;
    __shared__ float s_sum;
    int laneId = threadIdx.x % warpSize;
    // å½“å‰è¡Œ
    int row = blockIdx.x;
    if (row >= M) return;

    int iteration = CEIL(N, warpSize);  // æ¯ä¸ªçº¿ç¨‹è´Ÿè´£è®¡ç®—çš„æ•°æ®ä¸ªæ•°

    // æ±‚æ¯ä¸€è¡Œæœ€å¤§å€¼
    float max_val = -FLT_MAX;
    for (int i = 0; i < iteration; i++) {
        int col = i * warpSize + laneId;
        max_val = (col < N) ? fmaxf(max_val, input[row * N + col]) : max_val;
    }
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        max_val = fmaxf(max_val, __shfl_down_sync(0xFFFFFFFF, max_val, offset));
    }
    if (laneId == 0) s_max_val = max_val;  // æœ€å¤§å€¼æ±‡æ€»åˆ°ç¬¬ä¸€ä¸ªçº¿ç¨‹ï¼Œç¬¬ä¸€ä¸ªçº¿ç¨‹å°†å®ƒæ¬è¿åˆ°s_mem

    // æ±‚æ¯ä¸€è¡Œçš„å’Œï¼Œä¸”è¦å‡å»æœ€å¤§å€¼
    float sum = 0.0f;
    for (int i = 0; i < iteration; i++) {
        int col = i * warpSize + laneId;
        sum += (col < N) ? expf(input[row * N + col] - s_max_val) : 0.0f;
    }
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        sum += __shfl_down_sync(0xFFFFFFFF, sum, offset);
    }
    if (laneId == 0) s_sum = sum;  // sumå€¼æ±‡æ€»åˆ°ç¬¬ä¸€ä¸ªçº¿ç¨‹ï¼Œç¬¬ä¸€ä¸ªçº¿ç¨‹å°†å®ƒæ¬è¿åˆ°s_mem

    // è®¡ç®—æ¯ä¸€è¡Œçš„softmax
    for (int i = 0; i < iteration; i++) {
        int col = i * warpSize + laneId;
        if (col < N) output[row * N + col] = expf(input[row * N + col] - s_max_val) / s_sum;
    }
}
```

æ”¹ç”¨ `__shfl_xor_sync` åï¼Œæ¯ä¸ªçº¿ç¨‹çš„å¯„å­˜å™¨çš„ `max_val` å’Œ `sum` éƒ½æ˜¯æœ€ç»ˆçš„ç»“æœï¼Œå°±ä¸ç”¨å†™åˆ°å…±äº«å†…å­˜å†è¯»å–äº†ï¼š
```cpp
dim3 block(32);
dim3 grid(M);

__global__ void softmax_kernel(float* input, float* output, int M, int N) {
    int laneId = threadIdx.x % warpSize;
    // å½“å‰è¡Œ
    int row = blockIdx.x;
    if (row >= M) return;

    int iteration = CEIL(N, warpSize);  // æ¯ä¸ªçº¿ç¨‹è´Ÿè´£è®¡ç®—çš„æ•°æ®ä¸ªæ•°

    // æ±‚æ¯ä¸€è¡Œæœ€å¤§å€¼
    float max_val = -FLT_MAX;
    for (int i = 0; i < iteration; i++) {
        int col = i * warpSize + laneId;
        max_val = (col < N) ? fmaxf(max_val, input[row * N + col]) : max_val;
    }
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        max_val = fmaxf(max_val, __shfl_xor_sync(0xFFFFFFFF, max_val, offset));
    }

    // æ±‚æ¯ä¸€è¡Œçš„å’Œï¼Œä¸”è¦å‡å»æœ€å¤§å€¼
    float sum = 0.0f;
    for (int i = 0; i < iteration; i++) {
        int col = i * warpSize + laneId;
        sum += (col < N) ? expf(input[row * N + col] - max_val) : 0.0f;
    }
    for (int offset = warpSize >> 1; offset > 0; offset >>= 1) {
        sum += __shfl_xor_sync(0xFFFFFFFF, sum, offset);
    }

    // è®¡ç®—æ¯ä¸€è¡Œçš„softmax
    for (int i = 0; i < iteration; i++) {
        int col = i * warpSize + laneId;
        if (col < N) output[row * N + col] = expf(input[row * N + col] - max_val) / sum;
    }
}
```

è¿›ä¸€æ­¥åœ°ï¼Œ**å½“è¡Œæ•° M = 1ï¼Œé—®é¢˜é€€åŒ–ä¸ºå¯¹ä¸€ä¸ªé•¿åº¦ä¸º N çš„æ•°ç»„è¿›è¡Œå½’çº¦æ±‚å’Œ**ã€‚å¯ä»¥è‡ªè¡Œç¼–å†™ã€‚